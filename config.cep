character : type.

old_lady : character.
young_wife : character.
husband : character.
son : character.
daughter : character.
daughter_in_law : character.
maid : character.

object : type.

money : object.
weapon : object.

existsO object : pred.
existsC character : pred.

affection character character : pred.
attraction character character : pred.
anger character character : pred.

married character character : pred.
lovers character character : pred.
related character character : pred.
not_related character character : pred.

sad	character : pred.
mad character : pred.
dead character : pred.

has character object : pred.
wants character object : pred.

context init =
{
	existsO weapon,
	existsO weapon,

	existsC son,
	existsC old_lady,
	existsC daughter_in_law,
	existsC daughter,
	existsC maid,
	existsC young_wife,
	existsC husband,

	married husband young_wife, married young_wife husband,
	married son daughter_in_law, married daughter_in_law son,
	lovers son young_wife, lovers young_wife son,

	related son daughter, related daughter son,
	related husband son, related son husband,
	related daughter son, related son daughter,

	not_related son daughter_in_law, not_related daughter_in_law son,
	not_related maid son, not_related son maid,

	has husband money,
	wants daughter_in_law money,
	wants maid money,

	anger old_lady husband,
	anger son daughter,

	affection son daughter_in_law,
	affection son husband,
	affection son daughter,
	affection husband son
}.

stage play = {

get_weapon :
	existsO weapon * $existsC C
	-o has C weapon.

become_lovers :
	$attraction P P' * $attraction P' P * $not_related P P' * $not_related P' P
	-o lovers P P' * lovers P P'.

spend_time :
	$affection C' C * $affection C C'
	-o affection C' C * affection C C'.

fight :
	$anger C C'
	-o anger C' C * anger C C'.

make_up :
	anger C C' * anger C' C * $affection C C'
	-o .

murder_anger :
	$has P weapon *
	anger P P' * anger P P' * anger P P'
	-o !dead P'.

murder_cheating :
	$has P weapon *
	$married P P'' * lovers P' P''
	-o !dead P'.

murder_money :
	$has P weapon *
	wants P money * has P' money
	-o !dead P' * has P money.

steal_not_caught :
	wants C money * has C' money
	-o has C money.

steal_caught :
	$wants C money * $has C' money
	-o anger C' C * anger C' C * anger C' C.

get_married_for_love :
	$affection C C' * $affection C C' * $affection C C' *
	$affection C' C * $affection C' C * $affection C' C *
	$not_related C C' * $not_related C' C
	-o married C C'.

grief :
	affection C C' * dead C'
	-o sad C.

suicide :
	sad C * sad C * sad C * sad C * has C weapon
	-o !dead C.

} #interactive play.

%% theoretically you should be able to replace _ with a number to limit the steps taken
#trace _ play init. 
